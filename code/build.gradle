buildscript {
    ext {
        kotlin_version = "1.3.71"

        VERSION_NAME = "0.0.1"
        VERSION_CODE = 1

        MIN_SDK = 23
        TARGET_SDK = 29
    }


    repositories {
        google()
        jcenter()
    }

    dependencies {

        classpath "com.android.tools.build:gradle:4.0.0-beta04"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "org.sonarqube" version "2.8"
}

allprojects {

    repositories {
        google()
        jcenter()
    }
}

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io/"
        property "sonar.projectKey", "ARX-Vestibulum_pea-frontend"
        property "sonar.projectName", "PEA Frontend"
        property "sonar.version", VERSION_NAME
        property "sonar.scm.revision", getGitHash()
        property "sonar.sources", "src/main"
        property "sonar.tests", "src/test"
        property "sonar.exclusions", "**/*.png,**/*.jpg,**/*.webp"
        property "sonar.coverage.exclusions", "app/**"
        property "sonar.cpd.exclusions", "**/test/**"
    }
}

String getGitHash() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

task clean(type: Delete) { delete rootProject.buildDir }